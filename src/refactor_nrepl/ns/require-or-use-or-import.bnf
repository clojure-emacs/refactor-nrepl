S = require-form | use-form | import-form
require-form = <'(:require'> libspec* <')'>
use-form = <'(:use'> libspec* <')'>
import-form = <'(:import'> (class | classes-with-prefix)* <')'>
class = ns
classes-with-prefix = open prefix class* close
<libspec> = libspec-no-opts | libspec-with-opts | prefix-libspec
libspec-no-opts = open? ns close?
<ns> = #"[A-Za-z0-9_.?$%!-]+"
prefix-libspec = open prefix (libspec-with-opts | suffix)+ close
<prefix> = #"[A-Za-z0-9_.?$%!-]+"
suffix = #"[A-Za-z0-9_?$%!-]+"
libspec-with-opts = open ns opt* close
<sym> = #"[A-Za-z0-9_?.$%!*-]+"
<opt> = as | refer | only | flag | rename
<only> = ':only' sym-vec
<as> = ':as' sym
<refer> = ':refer' (sym-vec | ':all')
<rename> = ':rename' map
map = <'{'> entry* <'}'>
<entry> = sym sym
sym-vec = open sym+ close
<flag> = ':reload' | ':reload-all' | ':verbose'
<open> = <'['> | <'('>
<close> = <']'> | <')'>
